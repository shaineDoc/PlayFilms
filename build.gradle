plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.4'
    id 'io.spring.dependency-management' version '1.1.4'
    id 'org.openapi.generator' version '6.6.0'
}

group = 'ru.shaineDoc.desktop'
version = '0.0.1-SNAPSHOT'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring Starter
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'

    //JsonNullable
    implementation 'org.openapitools:jackson-databind-nullable:0.2.6'

    // PostgreSQL
    runtimeOnly 'org.postgresql:postgresql'

    // H2 (для тестов/локальной разработки)
    runtimeOnly 'com.h2database:h2'

    // Swagger/OpenAPI UI
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.5.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // MapStruct
    implementation 'org.mapstruct:mapstruct:1.5.5.Final'
    annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.5.Final'

    // Liquibase (опционально)
    implementation 'org.liquibase:liquibase-core'

    // Test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}
// Openapi generator
compileJava.dependsOn tasks.named("openApiGenerate")

openApiGenerate {
    generatorName = "spring"
    inputSpec = "$rootDir/src/main/resources/static/play-films-v0.0.1.yml"
    outputDir = "$buildDir/generated"
    apiPackage = "ru.shainedoc.api"
    modelPackage = "ru.shainedoc.model"
    invokerPackage = "ru.shainedoc.invoker"
    configOptions.set([
            dateLibrary         : "java8",
            useSpringBoot3      : "true",
            useTags             : "true",
            interfaceOnly       : "true",
            skipDefaultInterface: "true"
    ])
}

tasks.named('test') {
    useJUnitPlatform()
}
sourceSets {
    main {
        java {
            srcDir "$buildDir/generated/src/main/java"
        }
    }
}